#version 450

#define WORKGROUP_SIZE 16
layout ( local_size_x = WORKGROUP_SIZE, local_size_y = WORKGROUP_SIZE, local_size_z = 1 ) in;

layout(push_constant) uniform PushConstantsBlock
{
    int mWidth;
    int mHeight;
    float mhexSize; // Size in pixels
    float mTScale;
    vec4 mColorA;
    vec4 mColorB;
    vec4 mColorC;
    vec4 mColorD;
} PushConstants;

layout ( binding = 0 ) buffer InHexBuffer
{
    float mData[ 100 ];
} inHexData;
layout ( binding = 1, rgba8 ) uniform image2D outImage;

#define hash(p)  fract(sin(dot(p, vec2(12.9898, 78.233))) * 43758.5453)
#define hash2(p) fract(sin((p)*mat2(127.1,311.7, 269.5,183.3)) *43758.5453123)
#define hash3(p) fract(sin((p)*mat3(127.1,311.7, 74.7,  269.5,183.3,246.1,  113.5,271.9,124.6))*43758.5453123)
#define hash2x3(p) fract(sin((p)*mat3x2(127.1,311.7,  269.5,183.3,  113.5,271.9))*43758.5453123)

vec3 screenPosToHexCoord(vec2 position, float diameter)
{
    mat2 hexToScreenMatrix = mat2(-1, 0, -1.0 / 2.0f, sqrt(3.0f) / 2.0);

    vec2 posInHexSpace = inverse(hexToScreenMatrix) * (position / vec2(diameter));

    vec3 newPos;
    newPos.x = -posInHexSpace.x - posInHexSpace.y;
    newPos.y = posInHexSpace.x;
    newPos.z = posInHexSpace.y;

    // How much does the position deviate from a unit coord?
    vec3 roundDelta;
    roundDelta.x = abs(round(newPos.x) - newPos.x);
    roundDelta.y = abs(round(newPos.y) - newPos.y);
    roundDelta.z = abs(round(newPos.z) - newPos.z);

    // Recalculate the axis with the biggest error
    vec3 nodepos;
    if (roundDelta.z > roundDelta.x && roundDelta.z > roundDelta.y)
    {
        // Z biggest error
        nodepos.x = round(newPos.x);
        nodepos.y = round(newPos.y);
        nodepos.z = -nodepos.x - nodepos.y;
    } else if (roundDelta.y > roundDelta.x && roundDelta.y > roundDelta.z)
    {
        // Y biggest error
        nodepos.x = round(newPos.x);
        nodepos.z = round(newPos.z);
        nodepos.y = -nodepos.x - nodepos.z;
    } else
    {
        // X biggest error
        nodepos.y = round(newPos.y);
        nodepos.z = round(newPos.z);
        nodepos.x = -nodepos.y - nodepos.z;
    }

    return nodepos;
}

int calcHexIndex( vec3 inHexPos )
{
    vec3 sig = sign(inHexPos);
    vec3 shiftedpos = vec3(sig.z, sig.x, sig.y);
    vec3 mask = vec3(1) - max(sig * shiftedpos, vec3(0));

    vec3 u = mask * sig;

    mat2 transform = mat2(
    u.x, u.y,
    u.z, u.x
    );

    vec2 subCoord = inverse(transform) * inHexPos.xy - vec2(1, 0);
    int leg = 3 * int(u.x) + int(u.y);
    if (leg < 0) leg += 7;
    leg -= 1;

    if (inHexPos == vec3(0)) return 0;
    int index = 1 + 6 * int(subCoord.x) * (int(subCoord.x) + 1) / 2 + leg * int(subCoord.x + 1.0) + int(subCoord.y);

    return index;
}

float readHex( vec3 inHexPos )
{
    return inHexData.mData[ calcHexIndex( inHexPos ) ];
}

vec3 palette( in float t, in vec3 a, in vec3 b, in vec3 c, in vec3 d )
{
    return a + b*cos( 6.28318*(c*t+d) );
}

vec3 calcColor( vec2 inWorldPos )
{
    vec3 hexPos = screenPosToHexCoord( inWorldPos, float( PushConstants.mhexSize ) );
    float value = readHex( hexPos );
    value *= PushConstants.mTScale;

    return palette( value, PushConstants.mColorA.rgb, PushConstants.mColorB.rgb, PushConstants.mColorC.rgb, PushConstants.mColorD.rgb );
}

void main()
{
    ivec2 p = ivec2( gl_GlobalInvocationID.xy );
    if( p.x > PushConstants.mWidth || p.y > PushConstants.mHeight ) return;

    // Center origin
    vec2 worldPos = vec2( p ) - floor( vec2( PushConstants.mWidth, PushConstants.mHeight ) / 2.0f );
    worldPos += vec2( .5f );

    vec3 color = vec3( 0 );
    bool antialias = false;
    if( antialias )
    {
        color += calcColor( worldPos + vec2( -.25f, -.25f ) );
        color += calcColor( worldPos + vec2( .25f, -.25f ) );
        color += calcColor( worldPos + vec2( .25f, .25f ) );
        color += calcColor( worldPos + vec2( -.25f, .25f ) );
        color /= 4.0f;
    }
    else
    {
        color = calcColor( worldPos );
    }

    imageStore( outImage, p, vec4( color, 1 ) );
}